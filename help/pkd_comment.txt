PKDJ.SEDJ.from.paired.PK.results
  Copyright 1996, Warren D. Smith
=COPY()
复制
=新建(1)
新建
=SET.NAME("Pairedsht",GET.DOCUMENT(1))
设置workbook名
=ACTIVATE(Pairedsht)
激活workbook
=PASTE()
粘贴所有复制值至新workbook
=SELECT(!$A$1)
=COLUMN.WIDTH(5,!$A$1:!$K$1)
=COLUMN.WIDTH(6,!$C$1)
=COLUMN.WIDTH(6,!$G$1)
=COLUMN.WIDTH(6,!$K$1)
=COLUMN.WIDTH(4,!$D$1)
=COLUMN.WIDTH(4,!$H$1)
=COLUMN.WIDTH(4,!$L$1)
调整列宽
=SET.NAME("tst1",AND(!$A$1="PKm",TYPE(!$A$2)=1,!$B$1="PK",TYPE(!$B$5)=1,!$C$1="SE",TYPE(!$C$5)=1))
检查各列标题名与数值，type=1为全为数值。
=SET.NAME("tst2",AND(!$E$1="PKm",TYPE(!$E$2)=1,!$F$1="PK",TYPE(!$F$5)=1,!$G$1="SE",TYPE(!$G$5)=1))
=SELECT.END(4)
选择第一列最后一个数值
=SET.NAME("rA",GET.CELL(2))
获取A列行数
=SELECT(!$E$1)
=SELECT.END(4)
=SET.NAME("rB",GET.CELL(2))
获取E列行数
=IF(AND(tst1,tst2,rA>2,rB>2,rA=rB)=FALSE)
=  BEEP()
=  FORMULA("Worksheet contents incorrect.",!$I$2)
=  FORMULA("Cannot proceed.",!$I$3)
=  FORMULA("See PKDMACRO.Read.me.",!$I$4)
=  HALT()
=END.IF()
检验不通过报错
=SET.NAME("Ncases",rA-1)
=SET.NAME("PKA",!$B$5)
=SET.NAME("PKB",!$F$5)
=SET.NAME("SEA",!$C$5)
=SET.NAME("SEB",!$G$5)
变量赋值

=FORMULA("PKmD",!$I$1)
=FORMULA("PKD",!$J$1)
=FORMULA("SED",!$K$1)
=FORMULA("Group",!$L$5)
=FORMULA("ZD",!$L$6)
输出格式化
=SET.NAME("PKD",PKA-PKB)
=SET.NAME("SED",SQRT(SEA*SEA+SEB*SEB))
=SET.NAME("ZD",PKD/SED)
=SELECT(!$J$5:$K$6)
=FORMAT.NUMBER("0.000")
=FORMULA(PKD,!$J$5)
=FORMULA(SED,!$K$5)
=FORMULA(ZD,!$K$6)

=SET.NAME("SumD",0)
=SET.NAME("SSD",0)
=SELECT(OFFSET(!$I$2,0,0,Ncases,1))
=FORMAT.NUMBER("0.000")
=FOR("k",1,Ncases,1)
=SET.NAME("PKmD",OFFSET(!$A$1,k,0)-OFFSET(!$E$1,k,0))
=FORMULA(PKmD,OFFSET(!$I$1,k,0))
=SET.NAME("SumD",SumD+PKmD)
=SET.NAME("SSD",SSD+PKmD*PKmD)
=NEXT()

=SET.NAME("DF",Ncases-1)
=SET.NAME("PKDJ",Ncases*PKD-(DF/Ncases)*SumD)
=SET.NAME("SEDJ",SQRT((DF/Ncases)*(SSD-(1/Ncases)*SumD*SumD)))
=SET.NAME("TD",PKDJ/SEDJ)
=FORMULA("Pair",!$L$8)
=FORMULA("TD",!$L$9)
=FORMULA("DF",!$L$10)
=SELECT(!$J$8:$K$9)
=FORMAT.NUMBER("0.000")
=FORMULA(PKDJ,!$J$8)
=FORMULA(SEDJ,!$K$8)
=FORMULA(TD,!$K$9)
=FORMULA(DF,!$K$10)
=SELECT(!$K$11)
=FORMULA("DONE!",!$K$11)
=BEEP()
=BEEP()
=HALT()
